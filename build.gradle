buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "com.badlogicgames.gdx:gdx-tools:1.9.11"
    }
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker;

task texturePacker {
    doLast {
        def settings = new TexturePacker.Settings()
        settings.combineSubdirectories = true
        logger.info "Calling TexturePacker: "+texturePacker
        TexturePacker.processIfModified settings, "raw-img", "android/assets/img", "skin"
    }
}

task createProperties {
    doLast {
        new File("$projectDir/android/assets/bundle/properties.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['title'] = project.ext.title.toString()
            p['package-name'] = project.ext.packageName.toString()
            p.store w, null
        }
    }
}

task copyBundle(type: Copy) {
    from file("$projectDir/android/assets/bundle/bundle_en.properties")
    into file("$projectDir/android/assets/bundle")
    rename ".+", "bundle.properties"
}

allprojects {
    apply plugin: "eclipse"

    version = '1.0-dev'
    ext {
        appName = "Time Saver"
        gdxVersion = '1.9.11'
        roboVMVersion = '2.3.10'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
        title = 'Time Saver'
        packageName = 'com.wgsoft.game.timesaver'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java-library"


    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        
    }
}

project(":android") {
    apply plugin: "com.android.application"

    configurations { natives }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        
    }
}

project(":core") {
    apply plugin: "java-library"

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
    }

    classes {
        dependsOn texturePacker, createProperties, copyBundle
    }
}
